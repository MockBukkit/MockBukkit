/*
 * This build file was generated by the Gradle 'init' task.
 *
 * This generated file contains a commented-out sample Java project to get you started.
 * For more details take a look at the Java Quickstart chapter in the Gradle
 * user guide available at https://docs.gradle.org/4.2.1/userguide/tutorial_java_projects.html
 */

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'jacoco'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()

    maven {
        url 'https://hub.spigotmc.org/nexus/content/repositories/public/'
    }

    maven {
        url 'http://repo.md-5.net/content/groups/public/'
    }
}

dependencies {
    compile 'org.bukkit:bukkit:1.14.4-R0.1-SNAPSHOT'
    compile 'junit:junit:4.12'
    compile 'org.hamcrest:hamcrest-library:1.3'
    compile 'org.apache.commons:commons-io:1.3.2'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

group = 'com.github.seeseemelk'
version = '0.2.1-SNAPSHOT'

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment {
                MavenDeployment deployment -> signing.signPom(deployment)
            }
            repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
                authentication(userName: findProperty('ossrhUsername'), password: findProperty('ossrhPassword'))
            }
            snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
                authentication(userName: findProperty('ossrhUsername'), password: findProperty('ossrhPassword'))
            }
            pom.project {
                name 'MockBukkit-v1.14'
                packaging 'jar'
                description 'MockBukkit is a mocking framework for bukkit to allow the easy unit testing of Bukkit plugins.'
                url 'https://github.com/seeseemelk/MockBukkit'
                scm {
                    connection 'scm:git:git://github.com/seeseemelk/MockBukkit.git'
                    developerConnection 'scm:git:ssh://github.com:seeseemelk/MockBukkit.git'
                    url 'https://github.com/seeseemelk/MockBukkit/tree/v1.14-spigot'
                }
                licenses {
                    license {
                        name 'MIT License'
                        url 'https://github.com/seeseemelk/MockBukkit/blob/v1.14/LICENSE'
                    }
                }
                developers {
                    developer {
                        id 'seeseemelk'
                        name 'Sebastiaan de Schaetzen'
                        email 'sebastiaan.de.schaetzen@gmail.com'
                    }
                }
            }
        }
    }
}

eclipse.classpath.downloadJavadoc = true
eclipse.classpath.downloadSources = true